/* =====================================================================
   Procedure: dbo.sp_GetStaffSubjects
   Kind: RETRIEVE
   Purpose: Retrieve distinct subjects for staff based on faculty,
            department, phase, group, class, and academic year
   PageName: Page Materials_Quiz
   Ticket: SUBJECT-001
   Author: Abdelrahman
   Version: 1.0.0
   CreatedOn: 2025-12-27
   ===================================================================== */

IF OBJECT_ID('dbo.sp_GetStaffSubjects', 'P') IS NOT NULL
    DROP PROCEDURE dbo.sp_GetStaffSubjects;
GO

CREATE PROCEDURE dbo.sp_GetStaffSubjects
    -- [Input Parameters]
    @p_faculty_code   BIGINT,
    @p_department_id  BIGINT,
    @p_phase_id       BIGINT,
    @p_group_id       BIGINT,
    @p_class_id       BIGINT,
    @p_academic_year  BIGINT,
    @p_lang           NVARCHAR(10) = 'ar',

    -- [Output Parameters]
    @o_success_code   INT OUTPUT,
    @o_message        NVARCHAR(4000) OUTPUT
AS
BEGIN
    SET NOCOUNT ON;

    SET @o_success_code = -1;
    SET @o_message = N'Uninitialized';

    BEGIN TRY
        /* ================= Validation ================= */
        IF @p_faculty_code IS NULL
           OR @p_department_id IS NULL
           OR @p_phase_id IS NULL
           OR @p_group_id IS NULL
           OR @p_class_id IS NULL
           OR @p_academic_year IS NULL
        BEGIN
            SET @o_success_code = 4001;
            SET @o_message = CASE
                                WHEN LEFT(@p_lang,2) = 'ar'
                                THEN N'من فضلك ادخل جميع المحددات'
                                ELSE N'All parameters are required'
                             END;
            RETURN;
        END

        /* ================= Query ================= */
        SELECT DISTINCT
            S.SUBJECT_CODE,
            S.SUBJECT_NAME
        FROM dbo.E_BOOK_SUBJECT_TOTAL S
        INNER JOIN dbo.Staff_Material M
            ON S.SUBJECT_CODE = M.MaterialId
        WHERE
            S.FACULTY_CODE   = @p_faculty_code
            AND S.DEPARTMENT_ID = @p_department_id
            AND S.PHASE_ID      = @p_phase_id
            AND M.GroupId       = @p_group_id
            AND M.ClassId       = @p_class_id
            AND M.AcademicYear  = @p_academic_year;

        /* ================= Result Check ================= */
        IF @@ROWCOUNT = 0
        BEGIN
            SET @o_success_code = 4404;
            SET @o_message = CASE
                                WHEN LEFT(@p_lang,2) = 'ar'
                                THEN N'لا توجد مواد دراسية'
                                ELSE N'No subjects found'
                             END;
            RETURN;
        END

        /* ================= Success ================= */
        SET @o_success_code = 0;
        SET @o_message = CASE
                            WHEN LEFT(@p_lang,2) = 'ar'
                            THEN N'تم جلب البيانات بنجاح'
                            ELSE N'Success'
                         END;

    END TRY
    BEGIN CATCH
        SET @o_success_code = -ERROR_NUMBER();
        SET @o_message = N'Error: ' + ERROR_MESSAGE();
    END CATCH
END;
GO
