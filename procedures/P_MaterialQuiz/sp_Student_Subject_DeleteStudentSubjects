/* =====================================================================
   Procedure: dbo.sp_DeleteStudentSubjects
   Kind: DELETE
   Purpose: Delete student subject totals for a specific student and faculty
   PageName: Page Materials_Quiz
   Ticket: STUD-SUB-001
   Author: Abdelrahman Mamdouh
   Version: 1.0.0
   CreatedOn: 2025-12-25
   ===================================================================== */

IF OBJECT_ID('dbo.sp_DeleteStudentSubjects', 'P') IS NOT NULL
    DROP PROCEDURE dbo.sp_DeleteStudentSubjects;
GO

CREATE PROCEDURE dbo.sp_DeleteStudentSubjects
    -- [Input Parameters]
    @p_ed_stud_id   BIGINT,
    @p_faculty_code BIGINT,

    -- [Output Parameters]
    @o_success_code INT OUTPUT,
    @o_message      NVARCHAR(4000) OUTPUT
AS
SET NOCOUNT ON;

SET @o_success_code = -1;
SET @o_message = N'Uninitialized';

BEGIN TRY
    -- [Validation]
    IF @p_ed_stud_id IS NULL OR @p_faculty_code IS NULL
    BEGIN
        SET @o_success_code = 2001;
        SET @o_message = N'Student id and faculty code are required';
        RETURN;
    END

    -- [DML & Transactions]
    BEGIN TRAN;
        DELETE
        FROM dbo.E_BOOK_STUD_SUBJECT_TOTAL
        WHERE ED_STUD_ID   = @p_ed_stud_id
          AND FACULTY_CODE = @p_faculty_code;
    COMMIT TRAN;

    -- [Output]
    IF @@ROWCOUNT = 0
    BEGIN
        SET @o_success_code = 4404;
        SET @o_message = N'No records found to delete';
        RETURN;
    END

    SET @o_success_code = 0;
    SET @o_message = N'Student subjects deleted successfully';

END TRY
BEGIN CATCH
    IF XACT_STATE() <> 0 ROLLBACK TRAN;

    SET @o_success_code = -ERROR_NUMBER();
    SET @o_message = N'Error: ' + ERROR_MESSAGE();
END CATCH;
GO
